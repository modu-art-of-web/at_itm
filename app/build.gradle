// This file provides basic support for building the TensorFlow demo
// in Android Studio with Gradle.

apply plugin: 'com.android.application'
apply plugin: 'de.undercouch.download'

android {
    compileSdkVersion 25
    buildToolsVersion '26.0.2'

    lintOptions {
        abortOnError false
    }

    defaultConfig {
        applicationId = 'com.example.androidthings.imageclassifier'
        minSdkVersion 24
        targetSdkVersion 25
    }
    buildTypes {
        release {
            minifyEnabled = false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main {
            assets.srcDirs = ['./assets']
        }
    }
    android {
        packagingOptions {
            exclude 'project.properties'
            exclude 'META-INF/INDEX.LIST'
            exclude 'META-INF/NOTICE'
            exclude 'META-INF/LICENSE' // It is not include LICENSE file
            exclude 'META-INF/DEPENDENCIES'
        }
    }
    useLibrary 'org.apache.http.legacy'
}

// Download model zip file into ../../assets directory
// unzip it to demo project's own ./assets directory
import de.undercouch.gradle.tasks.download.Download
task downloadFile(type: Download) {
    src 'https://storage.googleapis.com/download.tensorflow.org/models/inception5h.zip'
    dest projectDir.toString() + '/../../assets/inception.zip'
}
task unzip(type: Copy) {
    from zipTree(projectDir.toString() + '/../../assets/inception.zip')
    into file(projectDir.toString() + '/assets')
}
unzip.dependsOn downloadFile

project.afterEvaluate {
    if (!(new File(projectDir.toString() + '/assets')).exists()) {
        preBuild.dependsOn unzip
    }
}

repositories {
    flatDir {
        dirs 'libs'
    }
}

dependencies {
    compile 'com.google.android.things.contrib:driver-button:0.3'
    compile 'org.tensorflow:tensorflow-android:1.2.0'
    //compile 'com.google.firebase:firebase-storage:10.2.6'
    compile 'com.google.firebase:firebase-core:11.0.1'
    compile 'com.google.firebase:firebase-storage:11.0.1'
    compile 'com.google.firebase:firebase-auth:11.0.1'
    compile 'org.apache.httpcomponents:httpcore:4.4.4'
    compile 'org.apache.httpcomponents:httpclient:4.5.2'
    compile 'org.apache.httpcomponents:httpmime:4.3.6'
    compile 'org.eclipse.paho:org.eclipse.paho.client.mqttv3:1.0.2'
    //compile 'com.wang.avi:library:2.1.3'
    compile 'com.github.ybq:Android-SpinKit:1.1.0'
    provided 'com.google.android.things:androidthings:0.4.1-devpreview'
}

apply plugin: 'com.google.gms.google-services'